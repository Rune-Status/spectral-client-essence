plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'maven-publish'
}

description = "Essence Gradle Plugin"

def mavenUsername = hasProperty('mavenUsername') ? mavenUsername : System.getenv('MAVEN_USERNAME')
def mavenPassword = hasProperty('mavenPassword') ? mavenPassword : System.getenv('MAVEN_PASSWORD')
def mavenUrl = hasProperty('mavenUrl') ? mavenUrl : System.getenv('MAVEN_URL')

gradlePlugin {
    plugins {
        essence {
            id = 'org.spectral.essence'
            displayName = 'Spectral Essence'
            implementationClass = 'org.spectral.essence.plugin.EssenceGradlePlugin'
        }
    }
}

dependencies {
    implementation gradleApi()
    implementation project(":deobfuscator")
    implementation project(":mapper")
    implementation project(":mapping")
    implementation project(":remapper")
    implementation "org.benf:cfr:$cfr"
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.spectral'
            artifactId = 'essence'
            version = version

            from components.java
            artifact sourceJar
        }
    }

    repositories {
        maven {
            credentials {
                username = mavenUsername
                password = mavenPassword
            }

            url = mavenUrl
        }
    }
}